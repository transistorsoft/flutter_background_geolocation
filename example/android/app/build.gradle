def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

// flutter_background_geolocation
Project background_geolocation = project(':flutter_background_geolocation')
apply from: "${background_geolocation.projectDir}/background_geolocation.gradle"

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'com.transistorsoft.flutterbackgroundgeolocationexample'
    }
    compileSdkVersion rootProject.ext.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'InvalidPackage'
    }
    // For testing Flavors
    // $ flutter run --flavor qa
    // $ flutter run --flavor dev
    /*
    flavorDimensions "app"
    productFlavors {
        hms {
          dimension "app"
          versionCode flutterVersionCode.toInteger()
          versionName flutterVersionName
        }
        qa {
            dimension "app"
            applicationIdSuffix ".qa"
            versionCode flutterVersionCode.toInteger()
            versionName flutterVersionName
        }
        dev {
            dimension "app"
            applicationIdSuffix ".dev"
            versionCode 1
            versionName "1.0"
        }
    }
    */

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "com.transistorsoft.flutterbackgroundgeolocationexample"
        minSdkVersion 22
        multiDexEnabled false
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86_64'
        }
    }
    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles "proguard-rules.pro"
            proguardFiles "${background_geolocation.projectDir}/proguard-rules.pro"
        }
    }
}


flutter {
    source '../..'
}

dependencies {
    /**
     * ENABLE FOR multidexEnabled
     *
    def multidex_version = "2.0.1"
    implementation "androidx.multidex:multidex:$multidex_version"
    */

    implementation "androidx.appcompat:appcompat:${rootProject.ext.appCompatVersion}"
}
