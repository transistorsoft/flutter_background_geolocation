group 'com.transistorsoft.flutter.backgroundgeolocation'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
    }
}


apply plugin: 'com.android.library'

def DEFAULT_COMPILE_SDK_VERSION                 = 28
def DEFAULT_PLAY_SERVICES_LOCATION_VERSION      = "16.0.0"
def DEFAULT_APP_COMPAT_VERSION                  = "1.1.0"
def DEFAULT_OK_HTTP_VERSION                     = "3.12.1"
def DEFAULT_ANDROID_PERMISSIONS_VERSION         = "0.1.8"
def DEFAULT_LOCAL_BROADCAST_MANAGER_VERSION     = "1.0.0"

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet('compileSdkVersion', DEFAULT_COMPILE_SDK_VERSION)

    defaultConfig {
        minSdkVersion 16
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

repositories {
    maven {
        url './libs'
    }
}

dependencies {
    def playServicesLocationVersion = safeExtGet('playServicesLocationVersion', DEFAULT_PLAY_SERVICES_LOCATION_VERSION)
    def appCompatVersion = safeExtGet('appCompatVersion', safeExtGet('supportLibVersion', DEFAULT_APP_COMPAT_VERSION))
    def okHttpVersion = safeExtGet('okHttpVersion', DEFAULT_OK_HTTP_VERSION)
    def androidPermissionsVersion = safeExtGet('androidPermissionsVersion', DEFAULT_ANDROID_PERMISSIONS_VERSION)
    def localBroadcastManagerVersion = safeExtGet('localBroadcastManagerVersion', DEFAULT_LOCAL_BROADCAST_MANAGER_VERSION)

    api(group: 'com.transistorsoft', name:'tslocationmanager', version: '+')
    implementation "com.google.android.gms:play-services-location:$playServicesLocationVersion"
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.github.tony19:logback-android:2.0.0'
    // android-permissions
    implementation "com.intentfilter:android-permissions:$androidPermissionsVersion"
    implementation "androidx.localbroadcastmanager:localbroadcastmanager:$localBroadcastManagerVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
}
